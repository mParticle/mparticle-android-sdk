buildscript {
    ext.kotlin_version = '1.9.0'
    ext.gradle_version = '8.1.4'

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.20"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jlleitschuh.gradle.ktlint" version "13.0.0"
}

sonarqube {
    properties {
        property "sonar.projectKey", "mParticle_mparticle-android-sdk"
        property "sonar.organization", "mparticle"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    throw new GradleException(
                        "Module '${project.name}' is missing an explicit namespace declaration. " +
                        "Please add 'namespace' to the android {} block in ${project.buildFile.name}"
                    )
                }
            }
        }
    }
    apply plugin: 'org.sonarqube'
    sonarqube {
        androidVariant "release"
    }
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    // Kits include their own `.editorconfig` files (some with `root = true`) which can
    // prevent the repository `.editorconfig` from being applied. To keep lint behavior
    // consistent without modifying kit repos, we override the relevant ktlint rules here.
    if (project.projectDir.toString().startsWith("${rootProject.projectDir}/kits/")) {
        project.plugins.withId("org.jlleitschuh.gradle.ktlint") {
            project.ktlint {
                additionalEditorconfig = [
                    "ktlint_standard_indent": "disabled",
                    "ktlint_standard_trailing-comma-on-call-site": "disabled",
                    "ktlint_standard_trailing-comma-on-declaration-site": "disabled",
                    "ktlint_standard_no-consecutive-blank-lines": "disabled",
                ]
            }
        }
    }
}

allprojects {
    group = 'com.mparticle'
    version = '5.78.1-SNAPSHOT'
    if (project.hasProperty('isRelease') && project.isRelease) {
        version = version.toString().replace("-SNAPSHOT", "")
    }

    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    // Apply shared signing configuration to all Android projects
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                signingConfigs {
                    debug {
                        storeFile file("${rootProject.projectDir}/debug.keystore")
                        storePassword "android"
                        keyAlias "androiddebugkey"
                        keyPassword "android"
                    }
                }
                buildTypes {
                    debug {
                        signingConfig signingConfigs.debug
                    }
                }
            }
        }
    }
}
