ext {
    kitDescription = 'mParticle Kit library for extending the Core mParticle SDK.'
}

apply plugin: 'com.android.library'
apply from: '../scripts/maven.gradle'
apply plugin: 'kotlin-android'


android {
    compileSdk 31
    testBuildType 'debug'

    defaultConfig {
        minSdkVersion 14
        targetSdk 31
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField 'Boolean', 'MP_DEBUG', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task kitSdkJavadocs(type: Javadoc) {
    include {
        String filePath = it.toString()
        filePath.contains('KitIntegration.java') ||
                filePath.contains('KitUtils')
    }
    source = android.sourceSets.main.java.srcDirs
    source += 'build/generated/source/buildConfig/release/'
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title = 'mParticle Android Kit API Reference'
    failOnError true
    getOptions().setNoTimestamp(true)
}

task generateJavadocsJar(type: Jar, dependsOn: kitSdkJavadocs) {
    archiveClassifier.set('javadoc')
    from kitSdkJavadocs.destinationDir
}

task generateSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    api project(':android-core')
    api 'androidx.annotation:annotation:[1.0.0,)'

    testImplementation 'junit:junit:4.13.2'
    testImplementation  files('libs/java-json.jar')
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-core:2.0.7'
    testImplementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation project(':testutils')

    androidTestImplementation project(':testutils')
}
