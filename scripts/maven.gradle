apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.kuali.maven.wagons:maven-s3-wagon:1.2.1"
}

allprojects {
    ext."signing.keyId" = System.getenv("mavenSigningKeyId")
    ext."signing.secretKeyRingFile" = System.getenv("mavenSigningKeyRingFile")
    ext."signing.password" = System.getenv("mavenSigningKeyPassword")
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def target_maven_repo
if (project.hasProperty('target_maven_repo')) {
    target_maven_repo = project.property('target_maven_repo')
}

uploadArchives{
    repositories {
        mavenDeployer {

            if (target_maven_repo == 's3') {
                configuration = configurations.deployerJars
                repository(url: 's3://maven.mparticle.com')
            } else if (target_maven_repo == 'sonatype') {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: System.getenv('sonatypeUsername'), password: System.getenv('sonatypePassword'))
                }
            } else {
                repository(url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath)
            }

            pom.project {
                version project.version
                artifactId project.name
                packaging 'aar'
                name project.ext.kitDescription
                description project.ext.kitDescription
                url 'https://github.com/mparticle/mparticle-sdk-android'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url 'https://github.com/mparticle/mparticle-android-sdk'
                    connection 'scm:git:https://github.com/mparticle/mparticle-android-sdk'
                    developerConnection 'scm:git:git@github.com:mparticle/mparticle-android-sdk.git'
                }

                developers {
                    developer {
                        id 'mParticle'
                        name 'mParticle Inc.'
                        email 'dev@mparticle.com'
                    }
                }
            }
        }
    }
}
