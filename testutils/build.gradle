apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.mparticle.testutils'
    testNamespace 'com.mparticle.legacyTest'
    compileSdk 33
    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(JAVA_VERSION)
        targetCompatibility = JavaVersion.toVersion(JAVA_VERSION)
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }
    defaultConfig {
        //This is set to 21 to avoid slow multidex.
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        if (legacyTest()) {
            testInstrumentationRunnerArgument 'annotation', 'com.mparticle.LegacyOnly'
        } else {
            testInstrumentationRunnerArgument 'notAnnotation', 'com.mparticle.LegacyOnly'
        }

        file("../android-core/src/androidTest/res/raw").mkdir()
        def jsSdkRawFile = file("../android-core/src/androidTest/res/raw/mparticle_js_sdk")
        jsSdkRawFile.createNewFile()
        if (project.hasProperty('js-sdk-file')) {
            def jsSdkPath = project.property('js-sdk-file')
            def jsSdkSourceFile = file(jsSdkPath)
            if (jsSdkSourceFile.exists() && jsSdkSourceFile.isFile()) {
                jsSdkRawFile.text = jsSdkSourceFile.text
                buildConfigField 'boolean', 'JS_TEST_SDK', 'true'
            } else {
                logger.warn("js-sdk-file was provided but does not exist or is not a file: ${jsSdkPath}. Falling back to remote JS SDK fetch.")
                buildConfigField 'boolean', 'JS_TEST_SDK', 'false'
            }
        } else {
            buildConfigField 'boolean', 'JS_TEST_SDK', 'false'
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lint {
        disable 'OutdatedLibrary'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly project(':android-core')
    compileOnly project(':android-kit-base')
    compileOnly 'org.mockito:mockito-android:5.8.0'

    api 'androidx.annotation:annotation:1.5.0'
    api 'androidx.test.ext:junit:1.1.4'
    api 'androidx.test:rules:1.5.0'

    androidTestCompileOnly project(':android-core')

    if (legacyTest()) {
        androidTestImplementation 'com.mparticle:android-core:4.17.1'
    } else {
        androidTestImplementation project(':android-core')

    }
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'com.google.firebase:firebase-messaging:20.0.0'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_version"
}

boolean legacyTest() {
    return project.hasProperty('legacy') ? project.property('legacy') : false
}
